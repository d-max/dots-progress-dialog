plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'dmax.dialog.spotsdialog'
    compileSdk 34

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

task sourcePackage(type: Jar) {
    archiveBaseName = ARTIFACT_ID
    archiveClassifier = "sources"
    archiveVersion = LIBRARY_VERSION_NAME
    from android.sourceSets.main.java.srcDirs
    destinationDirectory = file("$buildDir/artifacts")
}

task generatePom {
    doLast {
        pom {
            project {
                groupId "com.github.d-max"
                artifactId "spots-dialog"
                version LIBRARY_VERSION_NAME
                packaging "aar"
                name "spots progress dialog"
                description "Android AlertDialog with moving spots progress indicator"
                url "https://github.com/d-max/spots-dialog"
                scm {
                    url "https://github.com/d-max/spots-dialog"
                    connection "scm:git@github.com:d-max/spots-dialog.git"
                    developerConnection "scm:git@github.com:d-max/spots-dialog.git"
                }
                licenses {
                    license {
                        name "MIT License"
                        url "http://www.opensource.org/licenses/mit-license.php"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id "d-max"
                        name "Maksym Dybarskyi"
                        email "maxim.dybarskyy@gmail.com"
                    }
                }
            }
        }.writeTo("$buildDir/artifacts/${ARTIFACT_ID}-${LIBRARY_VERSION_NAME}.pom")
    }
}

task createArtifacts {
    dependsOn sourcePackage, generatePom
}

android.libraryVariants.all { variant ->
    if (variant.buildType.name == "release") {
        File outDir = file("$buildDir/artifacts")
        File apkFile = variant.outputs[0].outputFile

        def task = project.tasks.create("copy${variant.name.capitalize()}Aar", Copy)
        task.from apkFile
        task.into outDir
        task.rename apkFile.name, "${ARTIFACT_ID}-${LIBRARY_VERSION_NAME}.aar"

        task.dependsOn variant.assemble
        task.dependsOn sourcePackage
        createArtifacts.dependsOn task
    }
}